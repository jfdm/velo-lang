\begin{code}
module Code.MiniVelo

public export
\end{code}

%<*TyDef>
\begin{code}
data Ty = TyNat
        | TyBool
        | TyArr Ty Ty
\end{code}
%</TyDef>

\begin{code}
%hide Prelude.SnocList
\end{code}

%<*SnocListDef>
\begin{code}
data SnocList a = Lin
                | (:<) (SnocList a) a
\end{code}
%</SnocListDef>

%<*ElemTermDecl>
\begin{code}
data Elem : (gamma : SnocList ty) -> (a : ty) -> Type
data Term : (gamma : SnocList Ty) -> (a : Ty) -> Type
\end{code}
%</ElemTermDecl>

%<*ElemDecl>
\begin{code}
data Elem : (gamma : SnocList ty) -> (a : ty) -> Type
\end{code}
%</ElemDecl>
\begin{code}
 where
\end{code}
%<*varZero>
\begin{code}
  Here : ----------------------
          Elem (gamma :< ty) ty
\end{code}
%</varZero>

\end{code}
%<*varSuc>
\begin{code}
  There :  Elem gamma ty ->
          ---------------------
           Elem (gamma :< _) ty
\end{code}
%</varSuc>

\begin{code}
data Term : (gamma : SnocList Ty) -> (type : Ty) -> Type where
\end{code}

%<*inferenceZero>
\begin{code}
  Zero : ------------------
          Term gamma TyNat
\end{code}
%</inferenceZero>


%<*inferenceVar>
\begin{code}
  Var :  Elem gamma a ->
        --------------
         Term gamma a
\end{code}
%</inferenceVar>


%<*inferenceInc>
\begin{code}
  Inc :  Term gamma TyNat ->
        ------------------
         Term gamma TyNat
\end{code}
%</inferenceInc>


%<*inferenceApp>
\begin{code}
  App :  Term gamma (TyArr a b) ->
         Term gamma a ->
        ------------------------
         Term gamma b
\end{code}
%</inferenceApp>


%<*inferenceFunc>
\begin{code}
  Func :  Term (gamma :< a) b ->
         ------------------------
          Term gamma (TyArr a b)
\end{code}
%</inferenceFunc>


%<*Plus2Def>
\begin{code}
Plus2 : Term [<] (TyArr TyNat TyNat)
Plus2 = Func (Inc (Inc (Var Here)))
\end{code}
%</Plus2Def>

%<*IllScoped>
\begin{code}
failing "Mismatch between: ?gamma :< TyNat and [<]."

  Ouch : Term [<] TyNat
  Ouch = Var Here
\end{code}
%</IllScoped>

%<*IllTyped>
\begin{code}
failing "Mismatch between: TyBool and TyNat."

  Ouch : Term [<] (TyArr TyNat TyBool)
  Ouch = Func (Var Here)
\end{code}
%</IllTyped>
